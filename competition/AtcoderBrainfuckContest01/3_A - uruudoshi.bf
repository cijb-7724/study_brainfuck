方針
if n % 400 == 0
    YES
else if n % 100 == 0
    NO
else if n % 4 == 0
    YES
else
    NO

ABCD
X = 上2桁 = 10A plus B
Y = 下2桁 = 10C plus D

===

ABCD % 400 == 0
    X % 4 == 0 and Y == 0

ABCD % 100 == 0
    Y == 0

ABCD % 4 == 0
    Y % 4 == 0

===

bool_1 : X % 4 == 0
bool_2 : Y % 4 == 0
bool_3 : Y == 0

if bool_1 and bool_3
    YES
else if bool_3
    NO
else if bool_2
    YES
else
    NO

(0)A B C D
,>,>,>,
(0)A B C D;

(4) = 48;
>>+++++ +++
[-<+++++ +>]
<
(0)A B C D 48;

[
    -
    <-<-<-<-
    >>>>
]
(0)a b c d 0;

(4) : 上2桁
(5) : 下2桁

(4) = 10 times (0)
<<<<
[-
    >>>>+++++ +++++
    <<<<
]
(0)0; b c d 10a

(4) = (4) plus (1)
>[->>>+<<<]
(0)0 0; c d X

(5) = 10 times (2)
>
[-
    >>>+++++ +++++
    <<<
]
(0)0 0 0; d X 10c

(5) = (5) plus (3)
>[->>+<<]
(0)0 0 0 0; X Y

(6) : bool_1
(7) : bool_2
(8) : bool_3


(10) = copy_(4) using (9)
>
[->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]
>
(10)X; 0 0

n k k でn mod kの判定ができる
>++++>++++<<
n; 4 4
[
    ->>->[-]>[-]>[-]<<<
    [->+>+<<]>>[-<<+>>]<
    [[-]>+<]>
    >+<[[-]>-<]>
    [
        <<<<
        [->+>+<<]>>[-<<+>>]
        >>-
    ]
    <<<<<
]
>
[->-<]
+>[[-]<->]<

(10)0 0/1; 0 0 0 0
if (11) == 1
[
    -
    (11)0;
    true
    (6) = (10) and (10) == 0
    <<<<<+
    >>>>>
]
(11)0;



(10) = copy_(5) using (9)
<<<<< <
[->>>>+>+<<<<<]>>>>[-<<<<+>>>>]
>
(10)Y; 0 0

n k k でn mod kの判定ができる
>++++>++++<<
n; 4 4
[
    ->>->[-]>[-]>[-]<<<
    [->+>+<<]>>[-<<+>>]<
    [[-]>+<]>
    >+<[[-]>-<]>
    [
        <<<<
        [->+>+<<]>>[-<<+>>]
        >>-
    ]
    <<<<<
]
>
[->-<]
+>[[-]<->]<
(10)0 0/1; 0 0 0 0
if (11) == 1
[
    -
    (11)0;
    true
    (7) = (10) and (10) == 0
    <<<<+
    >>>>
]
(11)0;

<<<<< <<
[-]
(4)0;

>
(5)Y;

(8) = boolean_inversion_(5)
>>>+<<<
[
    [-]>>>-<<<
]



YESNO
ENOSY 
= 69|78|79|83|89
= 10 * (7|8|8|8|9)

(5)0;
+++++ +++++
[
    -
    <+++++ ++++
    <+++++ +++
    <+++++ +++
    <+++++ +++
    <+++++ ++
    >>>>>
]
(5)0;
<-<+++<-<--<-
(0)E; N O S Y 0 b1 b2 b3

(9) = copy_(8) and
(10) = copy_(8) using (11)
>>>>> >>>
[
    -
    >+
    >+
    >+
    <<<
]
>>>
[-<<<+>>>]

(11) = copy_(6) using (12)
<<<<<
[
    -
    >>>>>+
    >+
    <<<<< <
]
>>>>>>
[
    -
    <<<<< <+
    >>>>> >
]

(0)E N O S Y 0 b1 b2 b3 b3 b3 b1 0;

if (10) and (11)
    (12) = 1
<<
[
    [-]
    >
    [
        [-]
        >+<
    ]
    <
]
[-]>[-]<
(0)E N O S Y 0 b1 b2 b3 b3 0; 0 bA
<[->>+<<]
(0)E N O S Y 0 b1 b2 b3 0; 0 bB bA
<<[->>>+<<<]
(0)E N O S Y 0 b1 0; b3 0 bC bB bA
>>+
(0)E N O S Y 0 b1 0 b3 1; bC bB bA
<[-]<<[-]>>>
(0)E N O S Y 0 0 0 0 1; bC bB bA
>>>
(0)E N O S Y 0 0 0 0 1 bC bB bA;

[
    -<-<-<->>>
    <<<<< <<<.
    <<<<.
    >>>.
    >>>>> >>>>
]
<
[
    -<-<->>
    <<<<< <<<<<.
    >.
    >>>>> >>>>
]
<
[
    -<->
    <<<<< <.
    <<<<.
    >>>.
    >>>>> >>
]
<
[
    -
    <<<<< <<<.
    >.
    >>>>> >>
]
<
+++++ +++++.[-]



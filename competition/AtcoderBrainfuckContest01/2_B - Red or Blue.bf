方針
sを改行まで見ていく中で，それぞれの数を保持するセルをインクリメントする
r b
と個数を並べる
r 大なり bの判定ができればよい
    bからrを引いていく最中でbの部分が0になっていたらtrue
        r==bの場合最後にtrueになってしまう
        そこで事前にbに1を足しておけばよい


>>
(2) = n
,
(2) = (2)m10
----- -----
[
    (2)m38
    38 = 37 plus 1
    minus3 plus (7 time 37) = 0
    >---[<->+++++ ++]<-
    <<
    if (0)
        (1) = 10mul(0) and (0) = 0
    [
        -
        >++++++++++
        <
    ]

    (0);
    >>
    if (2)
        (1) = (2) and (2) = 0
    [
        -
        <+
        >
    ]

    <
    if (1)
        (0) = (1) and (1) = 0
    [
        -
        <+
        >
    ]
    
    >
    (2);
    ,----- -----
    (2) = nm10
]

(0)n 0 0;

<<[-]
>+>+
(0)0 1 1;

>
,
(2) = (2)m10
----- -----
[
    'B' = 66
    (2)m56
    ----- ----- ----- ----- 
    ----- ----- ----- ----- 
    ----- ----- ----- -

    if (x) = R
    >+<
    [
        it is R
        [-]>-<
        
        <[<]>
        (0)0 1;
        +
        (0)0 r;

        [>]
        (x)0;
    ]
    +
    (x)1;
    >
    else
    [
        it is B
        -
        (xp1)0;
        <[<]>>
        (0)0 r 1;
        +
        (0)0 r b;

        [>]
        (xp1)0;
    ]

   
    (xp1);
    ,----- -----
    (xp1) = nm10
]

(0)0 r b 1 1 ___ 0;
<[-<]
(0)0; rm1 bm1

>>+<<
(0)0; r' b
>
[
    -
    >-
    (0)0 rm1 bm1;
    (3) = copy_(2) using (4)
    [->+>+<<]>>[-<<+>>]
    (0)0 rm1 bm1 bm1 0;


    (4) = boolean_inversion(3)
    <
    >+<
    [[-]>-<]>
    (0)0 rm1 bm1 0 !bool;

    if (4)
        Yes
        (0) = 1
    [
        -
        <<<<+>>>>
    ]
    <<<
]
(1)0;

>[-]<<
(0)bool; 0 0

======================
Yes 89 101 115
10×(9 10 11)

>+<
[
    ->-<
    (0)0; 0
    >>
    (2)0;
    +++++ +++++
    [
        >>>>
        +++++[<++<++<++>>>-]
        <+<<-<-
    ]
    (2)0; 90 100 110 
    >-.>+.>+++++.
    (2)0; 89 101 115
    [-]<[-]<[-]<<<
    (0)0; 0
    +++++ +++++.[-]

]
>
[
    -
    (1)0;
    No 78 111
    11×(7 10)

    +++++ +++++ +
    [
        >>>
        * 9 9
        +++[<+++<+++>>-]
        * 7 10
        <+<--<-
    ]
    (1)0; 77 110
    
    >+.>+.
    (1)0; 78 111
    [-]<[-]<

    +++++ +++++.[-]

]



bf 20041219
+++++ +++++[
    -
    >>>>+++++ +++++
    >+++++ ++
    >+++++ ++
    >+++++ +++++
    >+++++ +++++ ++
    >+++++ +++++ ++
    <<<<< <<<<
]

>>>>+>---->>+++++>--->++<<<<< <<<<

+++++[
    -
    >>>>> >>>>>+
    >+
    >++
    >+++
    <<<<< <<<<< <<<
]
>>>>> >>>>>--

<<<<< <
[
    -
    <+
    >>>>> >>>>> >>>>+
    <<<<< <<<<< <<<
]
<
[
    -
    >+
    <
]

>>>>> >>>>>
[
    -
    >>+
    >>>>> >>>>>+
    <<<<< <<<<< <<
]
>>
[
    -
    <<+
    >>
]

<<<<<
[
    -
    >>>>>+
    >>>>> >>>>> >>>>> >>>+
    <<<<< <<<<< <<<<< <<<<< <<<
]
>>>>>
[
    -
    <<<<<+
    >>>>>
]

<<<<
[
    -
    >>>>+
    >>>>> >>>>> >>>>> >>>>> >>>>> >+
    <<<<< <<<<< <<<<< <<<<< <<<<< <<<<<
]
>>>>
[
    -
    <<<<+
    >>>>
]

<<<
[
    -
    >>>+
    >>>>> >>>>> >>>>> >>>>> >>>>> >>>>>
    >>>>> >>>>> >>+
    <<<<< <<<<< <<<<< <<<<< <<<<< <<<<<
    <<<<< <<<<< <<<<<
]
>>>
[
    -
    <<<+
    >>>
]

>+
[
    >
    [
        -
        >+
        <<<+
        >>
    ]
    <<
    [
        -
        >>+
        <<
    ]
    
    >
    [
        -
        >>>+
        <<<<+
        >
    ]
    <
    [
        -
        >+
        <
    ]
    
    >>>
    [
        -
        >>+
        <<
    ]
    >
    [
        -
        >-
        <
    ]
    
    >>+<
    [
        [-]
        >-<
    ]
    >
    
    >+<
    [
        [-]
        >-<
        <<<<<[-]
        >>>>>
    ]
    
    >
    [
        -
        
        <<<<< <
        [
            -
            >>>>> >>+
            >+
            <<<<< <<<
        ]
        >>>>> >>
        [
            -
            <<<<< <<+
            >>>>> >>
        ]
        
        >>
        [
            -
            <<+
            >>>+
            <
        ]
        <<
        [
            -
            >>+
            <<
        ]
        
        >
        [
            ->>->[-]>[-]>[-]<<<

            [
                -
                >+
                >+
                <<
            ]
            >>
            [
                -
                <<+
                >>
            ]
            <

            [
                [-]
                >+
                <
            ]
            >

            >+<
            [
                [-]
                >-
                <
            ]
            >

            [
                -
                <<<<

                [
                    -
                    >+
                    >+
                    <<
                ]
                >>
                [
                    -
                    <<+
                    >>
                ]

                >>
            ]
            <<<<<
        ]

        >
        [
            -
            >>+
            >+
            <<<
        ]
        >>>
        [
            -
            <<<+
            >>>
        ]
        
        <
        [
            -
            <-
            >
        ]
        
        +<
        [
            [-]
            >-
            <
        ]
        >
        
        [
            -
            >>>>> >>>>> >>>>> >>>>> >+
            <<<<< <<<<< <<<<< <<<<< <
        ]
        
        <<<<<
        <<<<< <

        [
            -
            <+
            >>>>> >>>>> >>>>> >>+
            <<<<< <<<<< <<<<< <
        ]
        <
        [
            -
            >+
            <
        ]

        >>>>> >>>>> >>>>> >>>
        [
            -
            >+
            <<<+
            >>
        ]
        <<
        [
            -
            >>+
            <<
        ]

        >
        [
            ->>->[-]>[-]>[-]<<<
            
            [
                -
                >+
                >+
                <<
            ]
            >>
            [
                -
                <<+
                >>
            ]
            <

            [
                [-]
                >+
                <
            ]
            >

            >+<
            [
                [-]
                >-
                <
            ]
            >
            
            [
                -
                <<<<
                
                [
                    -
                    >+
                    >+
                    <<
                ]
                >>
                [
                    -
                    <<+
                    >>
                ]

                >>
            ]
            <<<<<
        ]

        >
        [
            -
            >>+
            >+
            <<<
        ]
        >>>
        [
            -
            <<<+
            >>>
        ]

        <
        [
            -
            <-
            >
        ]

        +<
        [
            [-]
            >-
            <
        ]
        >

        [
            -
            >>>>> >>>>> >>>>+
            <<<<< <<<<< <<<<
        ]

        <<<<< <<<<< <<<
        
        <<<<< <
        
        [
            -
            <+
            >>>>> >>>>> >>>>> >>>>> >>>>>+
            <<<<< <<<<< <<<<< <<<<< <<<<
        ]
        <
        [
            -
            >+
            <
        ]
        
        >>>>> >>>>> >>>>> >>>>> >>>>> >
        [
            -
            >+
            <<<+
            >>
        ]
        <<
        [
            -
            >>+
            <<
        ]
        
        >
        [
            ->>->[-]>[-]>[-]<<<
            
            [
                -
                >+
                >+
                <<
            ]
            >>
            [
                -
                <<+
                >>
            ]
            <
            
            [
                [-]
                >+
                <
            ]
            >
            
            >+<
            [
                [-]
                >-
                <
            ]
            >
            
            [
                -
                <<<<
                
                [
                    -
                    >+
                    >+
                    <<
                ]
                >>
                [
                    -
                    <<+
                    >>
                ]

                >>
            ]
            <<<<<
        ]

        >
        [
            -
            >>+
            >+
            <<<
        ]
        >>>
        [
            -
            <<<+
            >>>
        ]

        <
        [
            -
            <-
            >
        ]
        
        +<
        [
            [-]
            >-
            <
        ]
        >
        
        [
            -
            >>>>> >>+
            <<<<< <<
        ]
        
        >>>>> >>>+
        <<<
        
        [
            -
            >[-]>[-]>[-]
            
            <<<<< <<<<< <<<<< <<<<<
            <<<<< <<<<< <<<<< <<<<<
            <<<<<
            
            .>.>>..<<<<.>>>.>..>>>.

            >>>>> >>>>> >>>>> >>>>>
            >>>>> >>>>> >>>>> >
        ]
        
        >
        [
            -
            >[-]>[-]

            <<<<< <<<<< <<<<< <<<<<
            <<<<< <<<<< <<<<< <<<<<
            <<<<<
            
            .>.>>..>>>.

            >>>>> >>>>> >>>>> >>>>>
            >>>>> >>>>> >>>>> >>
        ]
        
        >
        [
            -
            >[-]

            <<<<< <<<<< <<<<< <<<<<
            <<<<< <<<<< <<<<< <<<<<
            <<<<< <
            
            .>>>.>..>>>.

            >>>>> >>>>> >>>>> >>>>>
            >>>>> >>>>> >>>>> >>>
        ]
        
        >
        [
            -

            <<<<< <<<<< <<<<< <<<<<
            <<<<< <<<<< <<<<<

            [
                -
                <+
                >>>>> >>>>> >>>>> >>>>>
                >>>>> >>>>> >>>>> >>>>> >+
                <<<<< <<<<< <<<<< <<<<<
                <<<<< <<<<< <<<<< <<<<<
            ]
            <
            [
                -
                >+
                <
            ]
            
            >>>>> >>>>> >>>>> >>>>>
            >>>>> >>>>> >>>>> >>>>> >>
            [
                -
                >+
                <<<+
                >>
            ]
            <<
            [
                -
                >>+
                <<
            ]
            
            >
            [
                ->>->[-]>[-]>[-]<<<

                [
                    -
                    >+
                    >+
                    <<
                ]
                >>
                [
                    -
                    <<+
                    >>
                ]
                <
                
                [
                    [-]
                    >+
                    <
                ]
                >
                
                >+<
                [
                    [-]
                    >-
                    <
                ]
                >
                
                [
                    -
                    <<<<
                    
                    [
                        -
                        >+
                        >+
                        <<
                    ]
                    >>
                    [
                        -
                        <<+
                        >>
                    ]
                    
                    >>>+
                    <
                ]
                <<<<<
            ]
            
            >>>>> >>+++++ +++++
            
            <<<<<
            [
                -
                >>>>>-
                <<<<<
            ]
            
            >>>>
            [
                -
                >>+
                >+
                <<<
            ]
            >>>
            [
                -
                <<<+
                >>>
            ]
            
            <
            [
                [-]
                >+
                <
            ]
            >
            
            >>+++++ +++
            [
                -
                <+++++ +
                >
            ]
            <
            
            [
                -
                <<<+
                <+
                >>>>
            ]
            
            <
            [
                -
                <<<.
                >>>
            ]
            
            <<<[-]
            
            >.
            [-]
            
            +++++ +++++.[-]

            <<<<< <<<<< <<
        ]

        <<<<< <<<<< <<<<< <<<<<
        <<<<< <<<<
        <<<<< <
        +
        >>>>> >
    ]
    <<<<< <
]
